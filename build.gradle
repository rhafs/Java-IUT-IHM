apply plugin: "idea"
apply plugin: "java"

repositories {
    mavenCentral()
}

/*
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}
*/

dependencies {
    compile "com.intellij:forms_rt:7.0.3"
    runtime "com.intellij:forms_rt:7.0.3"
}

def jarPackage(artifactName, artifactVersion, artifactClass) {
    if (artifactVersion == "" || artifactVersion == null)
        artifactVersion = "1.0.0"

    return tasks.create("jar${artifactName}", Jar) {
        baseName = artifactName
        version = artifactVersion
        String className

        if (artifactClass == "" || artifactClass == null)
            className = baseName.capitalize()
        else
            className = artifactClass

        from(sourceSets.main.output) {
            include "$baseName/**"
        }

        from {
            configurations.compile.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }

        manifest {
            attributes "Implementation-Title": "$className",
                    "Implementation-Version": "$version",
                    "Main-Class": "$baseName.$className"
        }
    }
}

def jarPackage(artifactName, artifactVersion) {
    return jarPackage(artifactName, artifactVersion, "")
}

def jarPackage(artifactName) {
    return jarPackage(artifactName, "")
}

artifacts {
    archives jarPackage("aleatoire"), jarPackage("calculatrice", "1.2.3"), jarPackage("copier"),
            jarPackage("qcm"), jarPackage("ascii"), jarPackage("choix_ordinateur", "1.0.0", "ChoixOrdinateur"),
            jarPackage("listage"), jarPackage("saisie_au_vol", "1.0.0", "SaisieAuVol"), jarPackage("passage_arg", "1.0.0", "PassageArgs")
}
